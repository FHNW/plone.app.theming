#  Here is a list of scenarios and tests that we want to implement:
# https://raw.github.com/gist/3885011/8cd3e5141c12270757a667ce3812319f8c507cb6/gistfile1.txt
# Run with:
#  bin/test -s plone.app.theming -t theme_control_panel

*** Settings ***

Library  Selenium2Library  run_on_failure=Capture Page Screenshot
Variables  plone/app/testing/interfaces.py
Variables  plone/app/theming/tests/acceptance/variables.py

Suite Setup  Start browser
Suite Teardown  Close All Browsers


*** Test Cases ***

Scenario: Preview theme
    Given a logged-in admin user
     And a Plone site with no theme applied
    When I navigate to the "Theming" control panel
     And I click on the "Example theme" theme preview
    Then a preview of "Example theme" is shown in a new window

Scenario: Activate theme
    Given a logged-in admin user
     And a Plone site with no theme applied
    When I navigate to the "Theming" control panel
     And I click on the "Example theme" "Activate" button
    Then the "Example theme" is enabled in the site on all pages other than the "Theming" control panel

Scenario: Copy theme
    Given a logged-in admin user
     And a Plone site with no theme applied
    When I navigate to the "Theming" control panel
     And I click on the "Example theme" "Copy" button
     And I enter a new title and description
     And I click "Create"
    Then a new theme is created
     And I am taken to the theme editor for this theme

Scenario: Copy theme and enable immediately
    Given a logged-in admin user
     And a Plone site with no theme applied
    When I navigate to the "Theming" control panel
     And I click on the "Example theme" "Copy" button
     And I enter a new title and description
     And I select "enable immediately"
     And I click "Create"
    Then a new theme is created
     And the theme is enabled in the site on all pages other than the "Theming" control panel
     And I am taken to the theme editor for this theme

Scenario: Create new theme from scratch
    Given a logged-in admin user
     And a Plone site with no theme applied
    When I navigate to the "Theming" control panel
     And I click on the "New theme" button
     And I enter a new title and description
     And I click "Create"
    Then a new theme is created
     And I am taken to the theme editor for this theme

# TODO: Not working yet, since the unthemed site does not provide login
#Scenario: Create new theme from scratch and enable immediately
#    Given a logged-in admin user
#     And a Plone site with no theme applied
#    When I navigate to the "Theming" control panel
#     And I click on the "New theme" button
#     And I enter a new title and description
#     And I select "enable immediately"
#     And I click "Create"
#    Then a new theme is created
#     And the theme is enabled in the site on all pages other than the "Theming" control panel
#     And I am taken to the theme editor for this theme

*** Keywords ***

Start browser
    # We add an alias 'Main' so we can Switch back to it from other browsers
    Open browser  ${PLONE_URL}/plone/  alias=Main

a logged-in admin user
    Go to  ${PLONE_URL}/login_form
    Input text  __ac_name  ${SITE_OWNER_NAME}
    Input text  __ac_password  ${SITE_OWNER_PASSWORD}
    Click Button  Log in

a Plone site with no theme applied
    Go to  ${PLONE_URL}

I navigate to the "Theming" control panel
    Go to  ${PLONE_URL}/@@theming-controlpanel

I click on the "Example theme" theme preview
    Click Link  xpath=//div[@data-theme-title='Example theme']//a

a preview of "Example theme" is shown in a new window
#    Select Window  title='Preview: Example theme'
    Page should contain  Plone site

I click on the "Example theme" "Activate" button
    Click Button  Activate

the "Example theme" is enabled in the site on all pages other than the "Theming" control panel
    Page should not contain element  css=html head link[href*="++theme++example"]
    # TODO: Check a selection of other pages.
    Go to  ${PLONE_URL}
    Page should contain element  css=html head link[href*="++theme++example"]

I click on the "Example theme" "Copy" button
    Click Button  Copy

And I click on the "New theme" button
    Click Button  New theme

I enter a new title and description
    Input text  title  Test theme
    Input text  description  This is a test theme

I click "Create"
    Click Button  Create

a new theme is created
    Location Should Contain  /++theme++test-theme/

I am taken to the theme editor for this theme
    Location Should Contain  /++theme++test-theme/@@theming-controlpanel-mapper
    # This is not as robust as we'd like. Language and parameter dependent.
    Page should contain  Modify theme
    Page should contain  Test theme

I select "enable immediately"
    Select Checkbox   id=enableImmediately

the theme is enabled in the site on all pages other than the "Theming" control panel
    # We must create a new browser to verify theming, avoiding changing the Main browser page
    Open Browser  ${PLONE_URL}  alias=ThemedSite
    Given a logged-in admin user
    Page should contain element  css=html head link[href*="++theme++test-theme"]    
    Go to  ${PLONE_URL}/@@theming-controlpanel
    Page should not contain element  css=html head link[href*="++theme++test-theme"]
    Close Browser
    Switch Browser  Main
